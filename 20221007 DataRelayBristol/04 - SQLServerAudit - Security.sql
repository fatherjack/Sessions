-- Monitoring usage of the fn_get_audit_file access function
-- create a server audit 
use master
go
CREATE SERVER AUDIT [AUDIT-SQLAUDITACCESS]
TO FILE /* APPLICATION_LOG / SECURITY_LOG */
(	FILEPATH = N'C:\TEMP\SQLAudits\'
)
GO
-- the function exists in the master database so the audit Specification needs to be there
CREATE DATABASE AUDIT SPECIFICATION [Monitor-fn_get_audit_file access]
FOR SERVER AUDIT [AUDIT-SQLAUDITACCESS]
ADD (SELECT ON OBJECT::[sys].[fn_get_audit_file] BY [public])
GO

-- turn the audit on 
ALTER SERVER AUDIT [AUDIT-SQLAUDITACCESS]
WITH (STATE = ON)
go
ALTER DATABASE AUDIT SPECIFICATION [Monitor-fn_get_audit_file access]
WITH (STATE = ON)
go

-- view the audit to see if the audit is working ... 
-- TSQL (File audits)
SELECT 
	EVENT_TIME,
	ACTION_ID,
	IS_COLUMN_PERMISSION,
	SESSION_ID,
	SERVER_PRINCIPAL_ID,
	[DATABASE_PRINCIPAL_ID],
	TARGET_SERVER_PRINCIPAL_ID,
	TARGET_DATABASE_PRINCIPAL_ID,
	[OBJECT_ID],
	CLASS_TYPE,
	SESSION_SERVER_PRINCIPAL_NAME,
	SERVER_PRINCIPAL_NAME,
	SERVER_PRINCIPAL_SID,
	DATABASE_PRINCIPAL_NAME,
	TARGET_SERVER_PRINCIPAL_NAME,
	TARGET_SERVER_PRINCIPAL_SID,
	TARGET_DATABASE_PRINCIPAL_NAME,
	SERVER_INSTANCE_NAME,
	[DATABASE_NAME],
	[SCHEMA_NAME],
	[OBJECT_NAME],
	[STATEMENT]
FROM FN_GET_AUDIT_FILE('C:\TEMP\SQLAudits\AUDIT-SQLAUDITACCESS*.SQLAUDIT',DEFAULT,DEFAULT);  
